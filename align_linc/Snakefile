#!/usr/bin/env python

LINC_FASTA_URL = 'ftp://ftp.sanger.ac.uk/pub/gencode/Gencode_human/release_19/gencode.v19.lncRNA_transcripts.fa.gz'
LINC_FASTA = 'gencode.v19.lncRNA_transcripts.fa'

CHUNKS = 24

PREFIX = """
# force using global tmpdir
export TMPDIR=/scratch/biocore/mholtgr/tmp

# activate module command
. /etc/profile.d/modules.sh
module use /scratch/biocore/ostolpe/tools/easybuild/modules/all
"""

rule all:
    input:
        LINC_FASTA,
        'bwasw.alignments.bam',
        'bwamem.alignments.bam',
        'bowtie2.alignments.bam'

rule concat:
    input: ['transcripts/{{prog}}.{chunk:02d}.bam'.format(chunk=i) for i in range(CHUNKS)]
    output: '{prog}.alignments.bam'
    shell:
        """
        {PREFIX}

        module load BWA
        module load SAMtools

        samtools cat -o {output} {input}
        """

rule align_bwasw:
    input: 'transcripts/{chunk}.fa'
    output: 'transcripts/bwasw.{chunk}.bam'
    shell:
        r"""
        {PREFIX}

        module load BWA
        module load SAMtools

        bwa bwasw \
            -T 16 \
            -z 5 \
            /scratch/biocore/mholtgr/cubi_foundation/static_data/precomputed/BWA/0.7.11/hg19/2015-05-29/hg19.fa \
            {input} \
        | samtools view -Sb - \
        > {output}
        """

rule align_bwamem:
    input: 'transcripts/{chunk}.fa'
    output: 'transcripts/bwamem.{chunk}.bam'
    shell:
        r"""
        {PREFIX}

        module load BWA
        module load SAMtools

        bwa mem \
            -T 16 \
            /scratch/biocore/mholtgr/cubi_foundation/static_data/precomputed/BWA/0.7.11/hg19/2015-05-29/hg19.fa \
            {input} \
        | samtools view -Sb - \
        > {output}
        """

rule align_bowtie2:
    input: 'transcripts/{chunk}.fq'
    output: 'transcripts/bowtie2.{chunk}.bam'
    shell:
        r"""
        {PREFIX}

        module load Bowtie2
        module load SAMtools

        bowtie2 \
            --local \
            --mm \
            --very-sensitive-local \
            -x /scratch/biocore/mholtgr/cubi_foundation/static_data/precomputed/Bowtie2/2.2.5/GRCh37/g1k_phase1/human_g1k_v37 \
            -U {input} \
        | sed -e 's/SN:/SN:chr/' \
        | awk -F '\t' 'BEGIN {{OFS = FS}} {{ if ($1 ~ /^@SQ/) {{ print $0 }} else {{ $3 = "chr"$3; print $0 }} }}' \
        | samtools view -Sb - \
        > {output}
        """

rule to_fq:
    input: 'transcripts/{chunk}.fa'
    output: 'transcripts/{chunk}.fq'
    shell:
        """
        ./fasta_to_fastq.pl {input} >{output}
        """

rule split:
    input: LINC_FASTA
    output: ['transcripts/{chunk:02d}.fa'.format(chunk=i) for i in range(CHUNKS)]
    shell:
        """
        mkdir -p transcripts
        split --numeric-suffixes -l 2000 {LINC_FASTA} transcripts/
        for x in $(ls transcripts/* | grep -v 'fa'); do
            mv ${{x}} ${{x}}.fa
        done
        """

rule download:
    output: LINC_FASTA
    shell:
        """
        wget {LINC_FASTA_URL}
        gzip -d {LINC_FASTA}.gz
        """
